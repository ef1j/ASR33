010 REM MANDELBROT SET - BWBASIC
020 REM ITERATES Z _ Z^2 + C WHERE C IS IN THE REGION TO EXPLORE
030 DIM C$(72)
040 C$="-+=*35CV49FO6D#AZUGS02$%P&WXEQH8B@KRNM "
050 REM DIMENSIONS OF THE PRINTED PLOT (CHARACTERS)
060 X1=69
070 Y1=40
080 REM MAXIMUM NUMBER OF ITERATIONS
090 M = LEN(C$) * 10
100 REM CENTER COORDINATE OF THE PLOT, AND DIMENSIONS
110 R = 0.3577295363758484
120 I = 0.6421285085548648
130 D = 0.01
140 REM BOUNDARY BOX ON THE COMPLEX PLANE
150 R1 = R - D
160 R2 = R + D
170 I1 = I - (D * 4/3 * Y1/X1)
180 I2 = I + (D * 4/3 * Y1/X1)
190 S1=(R2-R1)/X1
200 S2=(I2-I1)/Y1
210 REM ITERATE EACH ROW IN THE PRINTED PLOT
220 FOR Y=0 TO Y1
230 I3=I1+S2*Y
240 REM ITERATE EACH COLUMN
250 FOR X=0 TO X1
260 R3=R1+S1*X
270 Z1=R3
280 Z2=I3
290 FOR N=1 TO M
300 A=Z1*Z1
310 B=Z2*Z2
320 IF A+B>4.0 THEN 380
330 Z2=2*Z1*Z2+I3
340 Z1=A-B+R3
350 NEXT N
360 REM THIS DIDN'T CONVERGE, SO FILL AS BLANK
370 N=LEN(C$)-1
380 E = N MOD LEN(C$) + 1
390 PRINT MID$(C$, E, 1);
400 NEXT X
410 PRINT
420 NEXT Y
430 END
